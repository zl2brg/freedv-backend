set(EBUR128_VERSION "1.2.6")

# Static libraries on some platforms need PIC
set(EBUR128_CMAKE_ARGS -DWITH_STATIC_PIC=1 -DBUILD_SHARED_LIBS=0)

if(APPLE)
    set(EBUR128_CMAKE_ARGS ${EBUR128_CMAKE_ARGS} -DCMAKE_AR=${CMAKE_AR} -DCMAKE_RANLIB=${CMAKE_RANLIB})
endif(APPLE)

if(CMAKE_CROSSCOMPILING)
    set(EBUR128_CMAKE_ARGS ${EBUR128_CMAKE_ARGS} -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE})
endif()

# Build ebur128 library 
include(ExternalProject)
ExternalProject_Add(build_ebur128
   SOURCE_DIR ebur128_src
   BINARY_DIR ebur128_build
   GIT_REPOSITORY https://github.com/jiixyj/libebur128.git
   GIT_TAG "v${EBUR128_VERSION}"
   GIT_SUBMODULES ""
   GIT_SUBMODULES_RECURSE NO
   CMAKE_ARGS ${EBUR128_CMAKE_ARGS}
   CMAKE_CACHE_ARGS -DCMAKE_BUILD_TYPE:STRING=Release -DCMAKE_OSX_DEPLOYMENT_TARGET:STRING=${CMAKE_OSX_DEPLOYMENT_TARGET} -DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
   # Idempotent: skip if already applied (ExternalProject re-runs PATCH on rebuild).
   PATCH_COMMAND ${CMAKE_COMMAND} -E env
                 bash -c "git apply --check \"${CMAKE_CURRENT_SOURCE_DIR}/cmake/Ebur128_CMake.patch\" 2>/dev/null && git apply \"${CMAKE_CURRENT_SOURCE_DIR}/cmake/Ebur128_CMake.patch\" || true"
   INSTALL_COMMAND ""
   UPDATE_DISCONNECTED 1
)

ExternalProject_Get_Property(build_ebur128 BINARY_DIR)
ExternalProject_Get_Property(build_ebur128 SOURCE_DIR)
add_library(ebur128 STATIC IMPORTED)
add_dependencies(ebur128 build_ebur128)

set(LIBEBUR128 "${BINARY_DIR}/libebur128${CMAKE_STATIC_LIBRARY_SUFFIX}")

set_target_properties(ebur128 PROPERTIES
    IMPORTED_LOCATION ${LIBEBUR128}
    IMPORTED_IMPLIB   "${BINARY_DIR}/libebur128${CMAKE_IMPORT_LIBRARY_SUFFIX}"
)

set(EBUR128_INCLUDE_DIRS ${CMAKE_CURRENT_BINARY_DIR}/ebur128_src/ebur128 ${CMAKE_CURRENT_BINARY_DIR}/ebur128_build)
include_directories(${EBUR128_INCLUDE_DIRS})
